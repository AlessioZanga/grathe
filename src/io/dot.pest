graphs = _{ SOI ~ graph+ ~ EOI }

graph = { strict_attribute ~ graph_type ~ graph_id ~ "{" ~ statements ~ "}" }

strict_attribute = { ^"strict"? }

graph_type = { ^"graph" | ^"digraph" }

graph_id = _{ ID? }

text = @{ (
    !ASCII_DIGIT ~ ("_" | ASCII_ALPHANUMERIC)+
    | quoted_text
) }

quoted_text = @{ "\"" ~ (!"\"" ~ "\\\""? ~ ANY)* ~ "\"" }

number = @{ "-"? ~ ("." ~ ASCII_DIGIT+ | ASCII_DIGIT+ ~ ("." ~ ASCII_DIGIT*)?) }

ID = _{ number | text }

html = { "<" ~ html_tag+ ~ ">" }

pair = { ID ~ "=" ~ (ID | html) }

statements = { statement* }

statement = _{ (pair | attributes | subgraph | edge | node) ~ ";"? }

attributes = { (graph_type | node_type | edge_type)? ~ "[" ~ (pair ~ (";" | ",")?)* ~ "]" }

subgraph_type = { ^"subgraph" }

subgraph = { (subgraph_type ~ ID?)? ~ "{" ~ statements? ~ "}" }

node_type = { ^"node" }

node_id = { ID ~ port? }

port = { (":" ~ ID ~ (":" ~ node_direction)? | ":" ~ node_direction) }

node_direction = { "n" | "ne" | "e" | "se" | "s" | "sw" | "w" | "nw" | "c" | "_" }

node = { node_id ~ attributes? }

edge_type = { ^"edge" }

edge_id = { (node_id | subgraph) ~ (edge_direction ~ (node_id | subgraph))+ }

edge_direction = { ("->" | "--") }

edge = { edge_id ~ attributes? }

html_tag = {
    table_tag
    | "<FONT>" ~ (html_text | table_tag) ~ "</FONT>"
    | "<I>" ~ (html_text | table_tag) ~ "</I>"
    | "<B>" ~ (html_text | table_tag) ~ "</B>"
    | "<U>" ~ (html_text | table_tag) ~ "</U>"
    | "<O>" ~ (html_text | table_tag) ~ "</O>"
    | "<SUB>" ~ html_text ~ "</SUB>"
    | "<SUP>" ~ html_text ~ "</SUP>"
    | "<S>" ~ html_text ~ "</S>"
}

html_text = { ("<BR/>" | (!"<" ~ ANY))+ ~ "<BR/>"* }

table_tag = { "<TABLE" ~ (ID ~ "=" ~ quoted_text)* ~ ">" ~ rows ~ "</TABLE>" }

rows = { row ~ ("<HR/>"? ~ row)* }

row = { "<TR>" ~ cells ~ "</TR>" }

cells = { cell ~ ("<VR/>" ~ cell)* }

cell = { ("<TD>" ~ (html_text | html_tag) ~ "</TD>" | "<TD>" ~ "<IMG/>" ~ "</TD>") }

WHITESPACE = _{ (" " | "\t" | NEWLINE) }

COMMENT = _{ ("/*" ~ (!"*/" ~ ANY)* ~ "*/"  | "//" ~ (!"\n" ~ ANY)* | "#" ~ (!"\n" ~ ANY)*) }
