graphs = _{ SOI ~ graph+ ~ EOI }

graph = _{ strict_type? ~ graph_type ~ ID? ~ "{" ~ statements? ~ "}" }

strict_type = { ^"strict" }

graph_type = { ^"graph" | ^"digraph" }

text = @{ (
    !ASCII_DIGIT ~ ("_" | ASCII_ALPHANUMERIC)+
    | "\"" ~ (!"\"" ~ "\\\""? ~ ANY)* ~ "\""
) }

number = @{ "-"? ~ ("." ~ ASCII_DIGIT+ | ASCII_DIGIT+ ~ ("." ~ ASCII_DIGIT*)?) }

ID = _{ number | text }

pair = { ID ~ "=" ~ ID }

statements = { statement+ }

statement = _{ (pair | attributes | subgraph | edge | node) ~ ";"? }

attributes = { (graph_type | node_type | edge_type)? ~ "[" ~ (pair ~ (";" | ",")?)* ~ "]" }

subgraph_type = { ^"subgraph" }

subgraph = { (subgraph_type ~ ID?)? ~ "{" ~ statements? ~ "}" }

node_type = { ^"node" }

node_id = { ID ~ port? }

port = { (":" ~ ID ~ (":" ~ node_direction)? | ":" ~ node_direction) }

node_direction = { "n" | "ne" | "e" | "se" | "s" | "sw" | "w" | "nw" | "c" | "_" }

node = { node_id ~ attributes? }

edge_type = { ^"edge" }

edge_id = { (node_id | subgraph) ~ (edge_direction ~ (node_id | subgraph))+ }

edge_direction = { ("->" | "--") }

edge = { edge_id ~ attributes? }

WHITESPACE = _{ (" " | "\t" | NEWLINE) }

COMMENT = _{ ("/*" ~ (!"*/" ~ ANY)* ~ "*/"  | "//" ~ (!"\n" ~ ANY)* | "#" ~ (!"\n" ~ ANY)*) }
